apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion
    defaultConfig {
        applicationId rootProject.ext.android.applicationId
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
        testInstrumentationRunner rootProject.ext.android.testInstrumentationRunner
        flavorDimensions "versionCode"
    }
    buildTypes {
        release {
            //不显示log
            buildConfigField "boolean", "LOG_DEBUG", "false"
            //Zipalign优化
            zipAlignEnabled false
            //代码混淆开关
            minifyEnabled false
            // 移除无用的resource文件
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    //多渠道打包
    productFlavors {
        //要打包的平台名字
        nameless {}
        /*   baidu {}
           qihu360 {}
           shoujizhushou {}*/
    }
    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [CHANNEL_VALUE: name]
    }
    //自定义打包时apk名字
    applicationVariants.all { variant ->
        variant.outputs.all {
            def fileName
            if (variant.buildType.name.equals('release')) {
                fileName = "${variant.productFlavors[0].name}.apk"
            } else if (variant.buildType.name.equals('debug')) {
                fileName = "${variant.productFlavors[0].name}.apk"
            }
            outputFileName = fileName
        }
    }


}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation rootProject.ext.dependencies["appcompat-v7"]
    implementation rootProject.ext.dependencies["constraint-layout"]
    testImplementation rootProject.ext.dependencies["junit"]
    androidTestImplementation rootProject.ext.dependencies["runner"]
    androidTestImplementation rootProject.ext.dependencies["espresso-core"]
    implementation rootProject.ext.dependencies["support-v4"]
    implementation rootProject.ext.dependencies["design"]

    //网络请求（retrofit+rxjava2）
    implementation 'com.squareup.okhttp3:okhttp:3.11.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.11.0'
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.9'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'com.google.code.gson:gson:2.8.5'

    //图片压缩
    implementation 'com.github.nanchen2251:CompressHelper:1.0.5'

    //进度对话框
    implementation 'com.github.limxing:Android-PromptDialog:1.1.3'
    //对话框lib
    implementation project(':selectordialog')
    //数据库
    implementation 'org.litepal.android:core:2.0.0'
    //查看数据库神器（PS:手机和电脑的网段必须一致，然后把logcat调到debug模式会有一个链接）
    debugImplementation'com.amitshekhar.android:debug-db:1.0.4'
}
